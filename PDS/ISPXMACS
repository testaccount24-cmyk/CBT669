*-
* ISPF interface macros
*
* Uses external macro SETREG
*
* History
*  2022-06-27  change $LA to SETREG
*
* Author
*  Willy Jensen
*  mail: willy@harders-jensen.com
*  web : http://harders-jensen.com
*-

*- ISPEXEC - general interface macro
         Macro
.* mode of operation
.*  Literal operands are upper-cased, if staring with '(' they are
.*  adresses as cl8'value', else as c'value'.
.*  Anything else is an address or a value.
&mlbl    IspExec &pl=
         lcla  &p,&sn
         lclc  &s,&ppl
         gblc  &ispxmacspl
         aif   (n'&syslist eq 0 and k'&pl ne 0).setpl
         aif   (k'&pl ne 0 or k'&ispxmacspl eq 0).ppl
&ppl     setc  '&ispxmacspl'
         ago   .ppln
.ppl     anop
&ppl     setc  '&pl'
         aif   (k'&pl gt 0).ppln
&p       seta  n'&syslist
&ppl     setc  '=&p.a(x''FFFFFFFF'')'
.ppln    anop
&mlbl    SETREG  r1,&ppl
&sn      seta  1
.a       anop
         aif   (&sn gt n'&syslist).b
&s       setc  '&syslist(&sn)'
         aif   (k'&s eq 0).null
         aif   ('&s '(1,1) eq '''').lit
         SETREG  r0,&s
         ago   .st
.null    anop
         la    r0,=c' '
         ago   .st
.lit     anop
&s       setc  Upper('&s')
         aif   ('&s '(1,1) eq '(').longlst
         la    r0,=cl8&s
         ago   .st
.longlst la    r0,=c&s
.st      anop
         st    r0,&p.(,r1)
&sn      seta  &sn+1
&p       seta  &p+4
         ago   .a
.b       oi    &p-4(r1),x'80'
.c       Call  ISPLINK
         ltr   r15,r15
         mexit
.setpl   anop
&ispxmacspl setc '&pl'
.x       Mend

*- VGET - specific interface macro
         Macro
.* CALL ISPLINK ('VCOPY',name,length-array,value-array,'MOVE')
.* sample
.*           l     r2,=a(1024*4)
.*           IspxVget vname,(vdata,(r2)),pl=ispxpl
.*           lr    r2,r0             length of received date
.*  vname    ds    cl8
.*  vdata    ds    xl(1024*4)
.*  ispxpl   ds    8a
         IspxVget &name,&data,&pl=
         SETREG  r1,&pl
         SETREG  r14,&data(2),l'&data(1)
         st    r14,20(,r1)
         la    r14,20(,r1)
         IspExec 'vcopy',&name,(r14),&data(1),'move',pl=&pl
         SETREG  r1,&pl
         l     r0,20(,r1)
         Mend

*- VPUT - specific interface macro
         Macro
.* CALL ISPLINK ('VREPLACE',name-list,lengths,values)
.* sample
.*           IspxVput 'address',address,pl=ispxpl
.*           la    r2,8
.*           IspxVput 'address',(address,(r2)),pl=ispxpl
.*           IspxVput 'address',(address,(r2)),pl=ispxpl
.*  ispxpl   ds    8a
.*  address  dc    c'Merianvej 11'
         IspxVput &name,&data,&pl=
         SETREG  r1,&pl
         SETREG  r14,&data(2),l'&data(1) length
         st    r14,20(,r1)
         la    r14,20(,r1)
         IspExec 'vreplace',&name,(r14),&data(1),pl=&pl
         Mend
