 /*                           IVP for REXXSTOR

   Testing actions:
     A          Acquire a data block.
     D          Delete (release) the storage.
     G          Get data from storage.
     GA         Get the address of the named storage area.
     GX         Same as G, plus the storage will be deleted.
     I          Get some information about the storage area.
     P          Write (put) data to storage. If the storageblock
     VER        Return the program name and build.
 */
 address TSO

 say '-------------------  version -------------------'

 v = RexxStor('ver')
 if left(v,8)<>'REXXSTOR' then exit xmsg('Version not pgmname:' v,8)
 say 'Version:' v
 say ''

 say '-----------  acquire, storage function ---------'

 cc   = Test('a','stblk0',2000,)          /* acquire storage         */
 addr = rv
 say 'address of stblk0:' addr
 cc   = Storage(addr,,'Kilroy was here')  /* write string to storage */
 resp = Storage(d2x(x2d(addr)+7),3)       /* get 'was'               */
 if resp<>'was' then exit xmsg('Storage string is not ''was''',8)
 cc   = Test('ga','stblk0')               /* get address             */
 if rv<>addr then exit xmsg('GA failed, addr:' addr',rv:' rv,8)
 say 'GA ok, addr' addr 'confirmed'
 say ''

 say '------------------ map and info ----------------'

 cc=Test('map')
 if rc<>0 then exit xmsg('Bad rc',20)

 cc=Test('i','stblk0')
 if rc<>0 then exit xmsg('Bad rc',20)
 if word(rv,1)<>'stblk0' then exit xmsg('Storage id not ''stblk0''',8)
 say ''

 say '----------------- put and get ------------------'

 text1 = 'another fine day'
 say   = 'text='text1
 cc=Test('p','stblk1',text1)
 if cc<>0 then exit xmsg('rc<>0',20)
 cc=Test('g','stblk1')                   /* return value */
 if cc<>0 then exit xmsg('rc<>0',20)
 if rv<>text1 then exit xmsg('Text not as expected',20)
 cc=Test('g','stblk1','newtext1')        /* value to variable */
 if cc<>0 then exit xmsg('rc<>0',20)
 say 'newtext1='newtext1
 if newtext1<>text1 then exit xmsg('Get text to var not as expected',20)
 say ''

 say '---------------- get&delete --------------------'

 cc=Test('i','stblk1')
 say 'info:' rv
 cc=Test('gx','stblk1')
 say 'data:' rv
 cc=Test('i','stblk1')
 if rv<>'' then exit xmsg('Storage stblk1 not deleted')
 say ''

 say '----------------  delete -----------------------'

 cc=Test('p','stblk3','Kilroy was here....')
 cc=Test('i','stblk3')
 say 'info:' rv
 cc=Test('d','stblk3')
 if cc<>0 then exit xmsg('Bad rc',20)
 cc=Test('i','stblk3')
 if rv<>'' then exit xmsg('Storage stblk3 not deleted')
 say ''

 say '------------------ map -------------------------'

 cc=Test('map')
 say ''

 say '---------------- invalid calls -----------------'

 cc= Test('i')
 if cc<>2 then exit xmsg('Bad rc' cc', should have been 2',20)
 say 'rc 2 received as expected'
 say ''

 exit xmsg('All ok')

Test:
 say '->REXXSTOR' arg(1)','arg(2)','arg(3)','arg(4)
 rv=RexxStor(arg(1),arg(2),arg(3),arg(4))
 rc=REXXSTOR_Rc+0
 rs=strip(REXXSTOR_RS,'b')
 say 'rv "'rv'" rc "'rc'" rs "'rs'"'
 return rc

XMsg: if arg(1)<>'' then say arg(1);return word(arg(2) 0,1)
